// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/RAM64.hdl
/**
 * Memory of sixty four 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    // use high 3 bits to select which RAM8 chip, low 3 bits as internal address
    DMux8Way(in=load, sel=address[3..5],
        a=ld0, b=ld1, c=ld2, d=ld3,
        e=ld4, f=ld5, g=ld6, h=ld7);

    RAM8(in=in, load=ld0, address=address[0..2], out=r0);
    RAM8(in=in, load=ld1, address=address[0..2], out=r1);
    RAM8(in=in, load=ld2, address=address[0..2], out=r2);
    RAM8(in=in, load=ld3, address=address[0..2], out=r3);
    RAM8(in=in, load=ld4, address=address[0..2], out=r4);
    RAM8(in=in, load=ld5, address=address[0..2], out=r5);
    RAM8(in=in, load=ld6, address=address[0..2], out=r6);
    RAM8(in=in, load=ld7, address=address[0..2], out=r7);

    Mux8Way16(a=r0, b=r1, c=r2, d=r3, e=r4, f=r5, g=r6, h=r7, sel=address[3..5], out=out);
}
